// standard menu definitions

bind ESCAPE [if (cleargui 1) [] [showgui main]]    // it all starts here

textlogo = [
	guifont "super" [ guitext "^faBlood Frontier" ]
	guilist [
		guistrut 5
		guifont "sub" [ guitext "^fdIt's Bloody Fun!" ]
	]
]

newgui main [
	guilist [
		guiimage "textures/emblem" [localconnect] 8 1 "textures/nothumb"
		guistrut 2
		guilist [
			textlogo
			guistrut 1
			if (> (ready) 0) [
				guibutton	"disconnect"		"disconnect"
				guistrut 1
			]
			if (> (getvote 0) 0) [
				guibutton	"show votes"		"showgui vote"
				guistrut 1
			]
			guibutton		"quick play" 		"localconnect"
			guibutton 		"play online"		"showgui servers"
			guibutton		"start game" 		"showgui game"
			guistrut 1
			guibutton		"options"			"showgui options"
			guibutton		"help"				"showgui help"
			guistrut 1
			guibutton		"quit"				"quit"	""
		]
	]
]

newgui loading [
	guilist [
		guiimage "" [] 8 1
		guistrut 2
		guilist [
			textlogo
			guistrut 1
			guifont "super" [
				if (&& (> (gamemode) 0) (strlen $maptitle)) [ guitext $maptitle ] [ guitext "Please Wait" ]
			]
			guifont "default" [
				guilist [
					guistrut 3;
					if (&& (> (gamemode) 0) (strlen $mapauthor)) [ guitext (format "by %1" $mapauthor) ] [ guitext " " ]
				]
			]
			guifont "sub" [
				if (> (gamemode) 0) [ guitext (gamename (gamemode) (mutators)) ] [ guitext " " ]
			]
			guistrut 2
			guilist [
				guistrut 32
				guifont "default" [ guiprogress $progressamt 3 ]
			]
		]
	]
]

ircident = 0

newgui irc [
	//initialisers, just so there's little complaining of missing aliases
	ircname@ircident = (getalias ircname@ircident)
	irchost@ircident = (getalias irchost@ircident)
	ircport@ircident = (getalias ircport@ircident)
	ircnick@ircident = (getalias ircnick@ircident)
	ircchan@ircident = (getalias ircchan@ircident)

	guistayopen [
		guitext "saved identities"
		guislider ircident 0 10
		guilist [
			guilist [
				guitext "network  "
				guitext "address  "
				guitext "port     "
				guitext "nickname "
				guitext "channel  "
			]
			guilist [
				guilist [
					guifield ircname@ircident -40
					guistrut 1
					guibutton "connect" [
						ircaddclient $ircname@ircident $irchost@ircident $ircport@ircident $ircnick@ircident
						ircaddchan $ircname@ircident $ircchan@ircident
					]
				]
				guifield irchost@ircident -40
				guifield ircport@ircident -40
				guifield ircnick@ircident -40
				guilist [
					guifield ircchan@ircident -40
					guistrut 1
					guibutton "join" [ ircaddchan $ircname@ircident $ircchan@ircident ]
				]
			]
		]
	]
	guibar
	ircgui
] [
	ircname0 = freenode
	irchost0 = irc.freenode.net
	ircport0 = 6667
	ircnick0 = (at (getname) 0)
	ircchan0 = #bloodfrontier
]

newgui about [
	guilist [
		guiimage "textures/emblem" [] 8 1 "textures/nothumb"
		guistrut 2
		guilist [
			textlogo
			guistrut 1
			guifont "emphasis" [ guitext "(C) 2006-2009" ]
			guifont "default" [ guitext [Anthony Cord and Quinton Reeves] ]
			guitext "http://bloodfrontier.com/"
			guistrut 1
			guitext "Based on Cube Engine 2 (C) 2001-2009"
			guitext [by Wouter "Aardappel" van Oortmerssen, Lee "Eihrul" Salzman]
			guitext "http://sauerbraten.org/"
		]
	]
]

newgui support [
	ircgui freenode
] [
	ircaddclient freenode irc.freenode.net 6667 (getname)
	ircaddchan freenode #bloodfrontier
]

newgui help [
	guilist [
		guiimage "textures/emblem" [] 8 1 "textures/nothumb"
		guistrut 2
		guilist [
			textlogo
			guistrut 1
			guibutton "about blood frontier" "showgui about"
			guibutton "get online support" "showgui support"
		]
	]
]

newgui test [guilist [loop i 20 [guilist [loop j 20 [textlogo]]]]]

showngamegui = 0
mapselected = 0
mutsselected = 0
modeselected = 4

vardef = [
	guitext (format "%1 " $arg2)
	do [@(format "%1val" $arg1) = $@arg1]
]
varfld = [
	varname = (format "%1val" $arg1)
	guifield $varname $arg2 [@arg1 $@varname]
]

newgui name [
	guilist [
		guiimage "textures/emblem" [] 8 1 "textures/nothumb"
		guistrut 2
		guilist [
			textlogo
			guistrut 1
			guititle "Welcome"
			guistrut 1
			guititle "Please enter your name"
			guistrut 1
			guilist [
				nameval = (getname)
				guifield nameval 20 [name $nameval]
				guistrut 1
				guibutton "^fgok" [cleargui 1] "action"
			]
		]
	]
]

weapname = [ "melee" "pistol" "shotgun" "smg" "flamer" "plasma" "rifle" "grenade" "rifle" "action" ] // make them all the same size

newgui arena [
	guilist [
		guiimage "textures/emblem" [] 8 1 "textures/nothumb"
		guistrut 2
		guilist [
			textlogo
			guistrut 1
			if (&& (> (mutators) 0) (& (mutators) 0x0020)) [
				guititle "arena weapon"
				guistrut 1
				guibutton "^fwrandom" [arenaweap 1] "action"
				guibutton "^fyshotgun" [arenaweap 2] "shotgun"
				guibutton "^fosmg" [arenaweap 3] "smg"
				guibutton "^frflamer" [arenaweap 4] "flamer"
				guibutton "^fcplasma" [arenaweap 5] "plasma"
				guibutton "^fvrifle" [arenaweap 6] "rifle"
				guistrut 1
				guibutton "^fgcancel" [cleargui 1] "action"
			] [
				guititle "sorry"
				guistrut 1
				guilist [ guistrut 1; guitext "^frthis is only available in arena" ]
				guistrut 1
				guibutton "^fgok" [cleargui 1] "action"
			]
		]
	]
	guistrut 1
	guilist [ guifont "sub" [ guitext (format "Press ^fs^fc%1^fS to open this menu at any time" (searchbinds "showgui arena" 5 "or")) ] ]
]

displayvote = [
	numvote = (getvote 0)
	if (> $numvote 0) [
		loop i $numvote [
			vote = (getvote (+ $i 1))
			voteplayer = (getclientname (at $vote 0))
			votemode = (at $vote 1)
			votemuts = (at $vote 2)
			votemap = (at $vote 3)
			votegame = (at $vote 1)
			guilist [
				guibutton "" (format "start %1 %2 %3" $votemap $votemode $votemuts) conopen
				guistrut 1
				guibutton (format "^fs%1^fS suggests ^fs^fy%2^fS on ^fs^fy%3^fS" $voteplayer (gamename $votemode $votemuts) $votemap) (format "start %1 %2 %3" $votemap $votemode $votemuts)
			]
		]
	] [ guitext "No votes are currently pending" ]
]
newgui vote [ displayvote ]

maplists = [ duelmaps duelmaps storymaps storymaps mainmaps mainmaps stfmaps stfmaps ctfmaps mctfmaps trialmaps trialmaps ]
maplistsize = 28 //adjust if desired
mapindex = 0
maplist = ""
mappath = ""
mapextra = ""
mapocta = 0

newgui game [
	if (|| (|| (!= $lastmode $modeselected) (!= $lastmuts $mutsselected)) (!= $showngamegui 1)) [
		if (&& (!= $lastmode $modeselected) (!= $showngamegui 0)) [ mutsselected = 0 ]
		maplist = ""
		mappath = ""
		if (> $modeselected 2) [
			if (& $mutsselected 0x0008) [ ms = 0 ] [
				ms = (* (- $modeselected 2) 2)
				if (& $mutsselected 0x0001) [ ms = (+ $ms 1) ]
			]
			do [list = $@(at $maplists $ms)]
			loop i (listlen $list) [
				lcurmap = (at $list $i)
				maplist = (concat $maplist $lcurmap)
				mappath = (concat $mappath (concatword "maps/" $lcurmap))
			]
		]
		maplist = (concat $maplist "*")
		mappath = (concat $mappath "*");
		loopfiles lcurmap maps bgz [
			if (< (listfind mcurmap $maplist [strcmp $mcurmap $lcurmap]) 0) [
				maplist = (concat $maplist $lcurmap)
				mappath = (concat $mappath (concatword "maps/" $lcurmap))
			]
		]
		if (&& (> $hasoctapaks 0) (> $mapocta 0)) [
			loopfiles lcurmap base ogz [
				maplist = (concat $maplist $lcurmap)
				mappath = (concat $mappath (concatword "base/" $lcurmap))
			]
		]
		maplist = (concat $maplist "?")
		mappath = (concat $mappath "?");
		if (!= $showngamegui 1) [
			mapnum = (listfind curmap $maplist [(|| (|| (strcmp $curmap $mapname) (strcmp (concatword "maps/" $curmap) $mapname)) (&& (&& (> $hasoctapaks 0) (> $mapocta 0)) (strcmp (concatword "base/" $curmap) $mapname)))])
		] [
			mapnum = (listfind curmap $maplist [(|| (|| (strcmp $curmap $mapcur) (strcmp (concatword "maps/" $curmap) $mapcur)) (&& (&& (> $hasoctapaks 0) (> $mapocta 0)) (strcmp (concatword "base/" $curmap) $mapcur)))])
		]
		if (&& (|| (< $mapnum 0) (= (strcmp "*" (at $maplist $mapnum)) 0)) (= (strcmp "maps/untitled" $mapname) 0)) [ mapnum = (- (listlen $maplist) 1); mapextra = $mapname ]
		lastmode = $modeselected
		lastmuts = $mutsselected
		mutsbefore = $mutsselected
		showngamegui = 1
	]
	if (strcmp (at $maplist $mapnum) "?") [ mapcur = $mapextra; mapimg = $mapextra ] [ mapcur = (at $maplist $mapnum); mapimg = (at $mappath $mapnum) ]
	guilist [
		guilist [
			if (< $lastmode 0) [
				guifont "default" [ guitext "game select" ]
				guifont "sub" [ guitext "^faplease select a mode and map to continue" ]
			] [
				guifont "default" [ guitext (gamename $modeselected $mutsselected) ]
				guilist [ guifont "sub" [ guitext "^faselected on " ]; guifont "default" [ guitext $mapcur ] ]
			]
			guistrut 1
			guilist [
				guilist [
					guititle "gamemode"
					guiradio "lobby"						modeselected 1
					guiradio "editing"						modeselected 2
					guiradio "story"						modeselected 3
					guiradio "deathmatch"					modeselected 4
					guiradio "secure-the-flag"				modeselected 5
					guiradio "capture-the-flag"				modeselected 6
					guiradio "time-trial"					modeselected 7
					guistrut 1
					guititle "mutators"
					if (>= $modeselected 1) [
						guibitfield "instagib"				mutsselected 0x0004
						guibitfield "arena"					mutsselected 0x0020
						if (>= $modeselected 4) [
							guibitfield "multi-sided"			mutsselected 0x0001
							guibitfield "teamplay"				mutsselected 0x0002
							if (= $modeselected 4) [
								guibitfield "duel"				mutsselected 0x0008
								guibitfield "survivor"			mutsselected 0x0010
							] [ guistrut 2 ]
						] [ guistrut 4 ]
					] [ guifont "radar" [ guitext "^fa^fenone available for this mode" ]; guistrut 5 ]
					mutstrying = 0
					if (!= $mutsselected $mutsbefore) [ loop q 6 [ if (&& (& $mutsselected (<< 1 $q)) (! (& $mutsbefore (<< 1 $q)))) [ mutstrying = (| $mutstrying (<< 1 $q)) ] ] ]
					mutsselected = (mutscheck $modeselected $mutsselected $mutstrying)
					mutsbefore = $mutsselected
				]
				guistrut 2
				guilist [ guiimage $mapimg [@(format "start %1 on %2" (gamename $modeselected $mutsselected) $mapcur)] 8 1 "textures/nothumb" ]
			]
			guistrut 2
			guilist [
				guilist [
					vardef botbalance		"bot balance"
					vardef botminskill		"bot min skill level"
					vardef botmaxskill		"bot max skill level"
				]
				guistrut 1
				guilist [
					varfld botbalance 12
					varfld botminskill 12
					varfld botmaxskill 12
				]
				guistrut 2
				guilist [
					guibutton "^fcadd bot" [addbot] "team"
					guibutton "^fmdel bot" [delbot] "player"
				]
			]
			if (> $hasoctapaks 0) [
				guistrut 4
				guilist [ guistayopen [
					guibutton "^fvshow maps from sauerbraten" [
						if (> $mapocta 0) [mapocta = 0] [mapocta = 1]
						showngamegui = 0
					] (? (> $mapocta 0) "checkboxon" "checkbox")
				] ]
			]
		]
		guistrut 2
		guilist [
			if (>= $lastmode 0) [
				guilist [
					mapscount = $maplistsize
					//if (> $hasoctapaks 0) [ mapscount = (- $mapscount 2) ]
					nummaps = (- (listlen $maplist) 1)
					mapindex = (min (max 0 (- $nummaps $mapscount)) $mapindex) //safeguard
					mapnum = (min $mapnum $nummaps)
					guilist [
						guilist [ guistrut 28 ]
						break = 0
						guilist [ guistrut 8; guifont "sub" [ guitext "^faavailable maps" ] ]
						loopwhile i $nummaps [if (= $break 0) [ 1 ] [ 0 ]] [
							if (&& (>= $i $mapindex) (< (- $i $mapindex) $mapscount)) [
								curmap = (at $maplist $i)
								if (strcmp $curmap "*") [
									if (= $modeselected 3) [
										guistrut (- $mapscount $i)
										nummaps = 1
										break = 1
									] [ guilist [ guistrut 7; guifont "radar" [ guitext "^fa^femaps not in the rotation" ] ] ]
								] [
									if (strcmp $curmap "?") [
										guistrut (- $mapscount $i)
										nummaps = 1
										break = 1
									] [ guiradio $curmap mapnum $i ]
								]
							] [
								if (&& (strcmp $curmap "*")  (= $modeselected 3)) [
									guistrut (- $mapscount $i)
									nummaps = 1
									break = 1
								]
							]
						]
					]
					guistrut 1
					guislider mapindex 0 (max (- $nummaps $mapscount) 0) [] 1
				]
				guilist [ guistrut 11; guifont "radar" [ guitext "^fa^fecustom map" ] ]
				guilist [
					guiradio "" mapnum $nummaps
					mapextraval = $mapextra
					guifield mapextraval 25 [mapextra = $mapextraval]
				]
			] [ guistrut (+ $maplistsize 3) ]
			guistrut 1
			guilist [
				guistrut 12
				guifont "super" [ guibutton "^fr^fecancel" [cleargui 1] ]
				guistrut 2
				guifont "super" [ guibutton "^fgvote" (format "start %1 %2 %3" $mapcur $modeselected $mutsselected) ]
			]
		]
	]
	guitab "vars"
	guilist [
		guilist [
			guilist [
				guilist [
					vardef maprotate			"map rotation"
					vardef spawnrotate			"spawn rotation"
					vardef selfdamage			"self damage"
					vardef trialdamage			"time-trial damage"
					vardef teamdamage			"team damage"
					vardef maxhealth			"max health"
					vardef extrahealth			"extra health"
					vardef maxcarry				"max carry weaps"
					vardef vampire				"vampire"
					vardef regenguard			"regen guard"
					vardef regenhealth			"regen health"
					vardef regenextra			"regen extra"
					vardef regenflag			"regen flag"
					vardef kamikaze				"kamikaze"
					vardef fraglimit			"frag limit"
					vardef ctflimit				"ctf score limit"
					vardef ctfstyle				"ctf style"
					vardef ctfresetdelay		"ctf reset delay"
					vardef stflimit				"stf score limit"
					vardef stfstyle				"stf style"
					vardef stffinish			"stf all finish"
					vardef stfpoints			"stf points"
					vardef stfoccupy			"stf occupy"
					vardef itemsallowed			"items allowed"
					vardef itemdropping			"item dropping"
					vardef spawnweapon			"spawn weapon"
					vardef instaweapon			"instagib weapon"
					vardef trialweapon			"time-trial weapon"
					vardef spawngrenades		"spawn grenades"
					vardef spawnprotect			"spawn protect"
					vardef spawndelay			"spawn delay"
					vardef instadelay			"insta spawn delay"
					vardef trialdelay			"time-trial spawn delay"
				]
				guistrut 2
				guilist [
					varfld maprotate 16
					varfld spawnrotate 16
					varfld selfdamage 16
					varfld trialdamage 16
					varfld teamdamage 16
					varfld maxhealth 16
					varfld extrahealth 16
					varfld maxcarry 16
					varfld vampire 16
					varfld regenguard 16
					varfld regenhealth 16
					varfld regenextra 16
					varfld regenflag 16
					varfld kamikaze 16
					varfld fraglimit 16
					varfld ctflimit 16
					varfld ctfstyle 16
					varfld ctfresetdelay 16
					varfld stflimit 16
					varfld stfstyle 16
					varfld stffinish 16
					varfld stfpoints 16
					varfld stfoccupy 16
					varfld itemsallowed 16
					varfld itemdropping 16
					guilist [
						guilist [ varfld spawnweapon 14 ]
						guistrut 1
						guistayopen [ guiimage (format "textures/%1" (at $weapname $spawnweapon)) [] 0.5 0 "textures/nothumb" ]
					]
					guilist [
						guilist [ varfld instaweapon 14 ]
						guistrut 1
						guistayopen [ guiimage (format "textures/%1" (at $weapname $instaweapon)) [] 0.5 0 "textures/nothumb" ]
					]
					guilist [
						guilist [ varfld trialweapon 14 ]
						guistrut 1
						guistayopen [ guiimage (format "textures/%1" (at $weapname $trialweapon)) [] 0.5 0 "textures/nothumb" ]
					]
					varfld spawngrenades 16
					varfld spawnprotect 16
					varfld spawndelay 16
					varfld instadelay 16
					varfld trialdelay 16
				]
			]
		]
		guistrut 3
		guilist [
			guilist [
				guilist [
					vardef gamespeed			"game speed"
					vardef forcegravity			"force gravity"
					vardef forcemovespeed		"force move speed"
					vardef forcemovecrawl		"force move crawl"
					vardef forcejumpspeed		"force jump speed"
					vardef forceimpulsespeed	"force impulse speed"
					vardef forceimpulsestyle	"force impulse style"
					vardef forceimpulsemeter	"force impulse meter"
					vardef forceimpulsecost		"force impulse cost"
					vardef forceimpulsecount	"force impulse count"
					vardef forceimpulseskate	"force impulse skate"
					vardef forceliquidspeed		"force liquid speed"
					vardef forceliquidcurb		"force liquid curb"
					vardef forceaircurb			"force air curb"
					vardef forcemovespeed		"force move speed"
					vardef damagescale			"damage scale"
					vardef hitpushscale			"hit push scale"
					vardef deadpushscale		"dead push scale"
					vardef wavepusharea			"wave push area"
					vardef wavepushscale		"wave push scale"
					vardef timelimit			"time limit"
					vardef triallimit			"time-trial limit"
					vardef intermlimit			"intermission limit"
					vardef votelimit			"vote wait limit"
					vardef duelreset			"duel resets players"
					vardef duelclear			"duel clears items"
					vardef duellimit			"duel wait limit"
					vardef itemspawntime		"item spawn time"
					vardef itemspawndelay		"item spawn delay"
					vardef itemspawnstyle		"item spawn style"
					vardef regendelay			"regen delay"
					vardef regentime			"regen time"
				]
				guistrut 2
				guilist [
					varfld gamespeed 16
					varfld forcegravity 16
					varfld forcemovespeed 16
					varfld forcemovecrawl 16
					varfld forcejumpspeed 16
					varfld forceimpulsespeed 16
					varfld forceimpulsestyle 16
					varfld forceimpulsemeter 16
					varfld forceimpulsecost 16
					varfld forceimpulsecount 16
					varfld forceimpulseskate 16
					varfld forceliquidspeed 16
					varfld forceliquidcurb 16
					varfld forcefloorcurb 16
					varfld forceaircurb 16
					varfld damagescale 16
					varfld hitpushscale 16
					varfld deadpushscale 16
					varfld wavepusharea 16
					varfld wavepushscale 16
					varfld timelimit 16
					varfld triallimit 16
					varfld intermlimit 16
					varfld votelimit 16
					varfld duelreset 16
					varfld duelclear 16
					varfld duellimit 16
					varfld itemspawntime 16
					varfld itemspawndelay 16
					varfld itemspawnstyle 16
					varfld regendelay 16
					varfld regentime 16
				]
			]
			guistayopen [ guibutton "^foreset all game variables" [resetvars] ]
		]
	]
	if (> (getvote 0) 0) [
		guitab "votes"
		guilist [ guilist [ displayvote ] ]
	]
] [
	if (< (gamemode) 2) [ modeselected = $defaultmode ] [ modeselected = (gamemode) ]
	if (< (mutators) 0) [ mutsselected = $defaultmuts ] [ mutsselected = (mutscheck $modeselected (mutators)) ]
	mutsbefore = $mutsselected
	lastmode = -1
	lastmuts = -1
	mapnum = 0
	mapextra = ""
	showngamegui = 0
]

newgui servers [
	guistayopen [
		guilist [
			guibutton "update "						updatefrommaster
			guibutton "reset "						clearservers
			guibutton "default sort "				serversortreset
			guicheckbox "search lan "				searchlan
            guibar
            guibutton "disconnect "                 disconnect server
            guibutton "lan "                        lanconnect          server
		]
	]
	guiservers
]

showfileeditor = [
    guinoautotab [
        guieditor $arg1 $arg2 $arg3
        guistayopen [
            guilist [
                guibutton "load" [textfocus @arg1; textload @arg1]
                guibar
                guibutton "save" [textfocus @arg1; textsave @arg1]
                guibar
                guibutton "exec" [textfocus @arg1; textexec]
                guibar
                guibutton "copy" [textfocus @arg1; textcopy]
                guibar
                guibutton "paste" [textfocus @arg1; textpaste]
                guibar
                guibutton "select" [textfocus @arg1; textselectall]
                guibar
                guibutton "clear" [textfocus @arg1; textclear]
            ]
        ]
    ]
]

notepadfile = "untitled.txt"

newgui notepad [
    guifield notepadfile -30
    showfileeditor $notepadfile -80 20
]

notepad = [
    if (> $numargs 0) [notepadfile = $arg1]
    showgui notepad
]

newgui pastebuffer [
    guinoautotab [
        guieditor "#pastebuffer" -80 20
        guistayopen [
            guilist [
                guibutton "exec" [textfocus "#pastebuffer"; textexec]
                guibar
                guibutton "clear" [textfocus "#pastebuffer"; textclear]
            ]
        ]
    ]
]

pastebuffer = [showgui pastebuffer]

newgui scratchpad [
    guinoautotab [
        guieditor "#scratchpad" -80 20
        guistayopen [
            guilist [
                guibutton "exec" [textfocus "#scratchpad"; textexec]
                guibar
                guibutton "copy" [textfocus "#scratchpad"; textcopy]
                guibar
                guibutton "paste" [textfocus "#scratchpad"; textpaste]
                guibar
                guibutton "select" [textfocus "#scratchpad"; textselectall]
                guibar
                guibutton "clear" [textfocus "#scratchpad"; textclear]
            ]
        ]
    ]
]

scratchpad = [showgui scratchpad]

doinsel = 0

findents = [
	match = 1
	if $doinsel [
		if (= $doinsel 1) [
			match = (* $match (insel))
		] [
			match = (* $match (! (insel)))
		]
	]
	if $dosearch0 [
		match = (* $match (strcmp (et) (at $enttypelist $entt)))
	]
	loop i 5 [
		if (getalias (concatword "dosearch" (+ $i 1))) [
			match = (* $match (= (ea (+ $i 1)) (getalias (concatword "enta" (+ $i 1)))))
		]
	]

	result $match
]

entt = 0
enta1 = 0
enta2 = 0
enta3 = 0
enta4 = 0
enta5 = 0

dosearch0 = 1
dosearch1 = 1
dosearch2 = 1
dosearch3 = 1
dosearch4 = 1
dosearch5 = 1

newgui entfind [
	guitext "Set the attributes below to search for"
	guistrut 1
	guicheckbox "Type (see next tab)" dosearch0
	loop i 5 [
		guilist [
			guicheckbox [attr @(+ $i 1)] [dosearch@(+ $i 1)]
			guistrut 1
			guifield [enta@(+ $i 1)] 11 [ enta@(+ $i 1) = (min 0x7FFFFFFF (max -0x7FFFFFFF $enta@(+ $i 1)))]
		]
	]

	guilist [
		guiradio "off" doinsel 0
		guistrut 1
		guiradio "inside selection" doinsel 1
		guistrut 1
		guiradio "outside selection" doinsel 2
	]

	guibar
	guistayopen [
		guibutton "Copy Attributes from currently selected entity" [
			loop i 5 [
				[enta@(+ $i 1)] = (ea (+ $i 1))
			]
			loop i (listlen $enttypelist) [
				if (strcmp (et) (at $enttypelist $i)) [
					entt = $i
				]
			]
		]
		guibutton "select all valid entities" [entselect [(findents)]]
	]

	guitab Type
	guilist [
		loop i (+ (div (listlen $enttypelist) 10) 1) [
			guilist [
				loop j (min (- (listlen $enttypelist) (* 10 $i)) 10) [
					guiradio (at $enttypelist (+ $j (* $i 10))) entt (+ $j (* $i 10))
				]
			]
		]
	]
]

newmapsize = 12
savemap_name = temp

newgui editing [
    guibutton "materials.."                           "showgui materials"
    guibutton "find entities"				"showgui findent"
    guibutton "toggle edit mode	(key F1)"              "toggleedit"
    guibutton "toggle heightmap	(key H)"              ""
    guibutton "deselect		(key SPACE)"                "cancelsel"
    guibutton "passthrough	(hold ALT)"               "passthrough"
    guibutton "select		drag left mouse   "	          ""
    guibutton "select corners	drag middle mouse "     ""
    guibutton "extend selection	right mouse"          ""
    guibutton "reorient		right mouse"                ""
    guibutton "move selection	drag right mouse"       ""
    guibutton "copy		(key C)"			                  "copy"
    guibutton "paste		(key V)"			                "paste"

    guitab ents
    guilist [
      guilist [
        guibutton "drop entities.."              "showgui dropent"
        guibutton "mapmodels.."                  "showgui mapmodels"
        guibutton "light.."                      "resetlight;showgui newlight"

        guibutton "newent playerstart   "
        guibutton "newent weapon"
      ]
      guilist [
        guibutton "teleport.."                   "showgui newteleport"
      ]
    ]

    guitab keys
    guibutton "flip / mirror cube	(key X)"            "flip"
    guibutton "undo		(key Z, U)"                     "undo"
    guibutton "redo		(key I)"                        "redo"
    guibutton "delete		(BACKSPACE)"                  "editdel"
    guibutton "texture palette     (F2)" "showtexgui"
    guibutton "mapmodels list			(F4)" "showgui mapmodels"
    guibutton "select all in box	(N)" "entselect insel"
    guibutton "select all matching	(M)" "selentfindall"
    guibutton "center view on ent	(COMMA + scroll)" "entautoview"
    guibutton "edit entity console	(PERIOD)" "selentedit"
    guibutton "toggle heightmap tex	(MMB in heightmapmode)" ""
    guicheckbox "toggle outline		(key 7)"        outline
    guicheckbox "toggle wireframe		(key 8)"    wireframe


    guitab scroll
    guitext "push/pull selection		scroll wheel only"
    guitext "push the face of a cube	hold F + scroll wheel"
    guitext "change gridsize		hold G + scroll wheel"
    guitext "change textures		hold Y + scroll wheel"
    guitext "rotate         		hold R + scroll wheel"
    guitext "push corner with cursor	hold Q + scroll wheel"
    guitext "push/pull heightmap                hold LCTRL + scroll wheel"
    guitext "change heightmap brush	hold B + scroll wheel"

    guitab light

    guibutton "recompute lights (SLOWEST: 8xAA)"			"calclight 3"
    guibutton "recompute lights (SLOW: 4xAA)"				"calclight 2"
    guibutton "recompute lights (FAST: 2xAA)"				"calclight 1"
    guibutton "recompute lights (FAST: no AA)"				"calclight 0"
    guibutton "patch lighting"								"patchlight"

	guilist [
		guicheckbox "cast shadows (*)" lmshadows
		guicheckbox "fullbright" fullbright
	]
	guitext "model shadows"
	guilist [
		guiradio "full (*)"		mmshadows 2
		guiradio "normal"	mmshadows 1
		guiradio "none"		mmshadows 0
	]

	guitext "lightprecision (default: 32)"
    guislider lightprecision

    guitext "lighterror (default: 8)"
    guislider lighterror

    guitab mapop
    guitext   Savemap:
    guifield  savemap_name 20 [ savemap $savemap_name ]
    guibutton [load @savemap_name map]             "map $savemap_name"
    guibutton "newmap (default size: 12)"          "newmap $newmapsize"
    guislider "newmapsize" 10 16
    guibutton "increase mapsize (2x)"              "mapenlarge"
    guitext   "max undo size (default: 5 MB)"
    guislider "undomegs" 1 10
    guibutton "optimize geometry"                  "remip"
    guibutton "set map title/author"               "saycommand [/mapmsg ]"

    guitab cfg
    guitext (format "%1.cfg" $mapname)
    showfileeditor (format "%1.cfg" $mapname) -50 12
]

newgui materials [
    guibutton "air"                        "editmat air"
    guibutton "water"                      "editmat water"
    guibutton "lava"                       "editmat lava"
    guibutton "clip"                       "editmat clip"
    guibutton "glass"                      "editmat glass"
    guibutton "noclip"                     "editmat noclip"
    guibutton "aiclip"                     "editmat aiclip"
    guibutton "death"                      "editmat death"

    guicheckbox "show material volumes"    showmat
]

setting_entediting = 1

newgui dropent [
    guitext "drop entities:"
    guiradio "to the floor"                 entdrop 1
    guiradio "on selection"                 entdrop 2
    guiradio "to the floor at selection"    entdrop 3
    guiradio "at current position"          entdrop 0
    guibar
    guicheckbox "entity snap-to-grid"       entselsnap
    guicheckbox "entity selection"          setting_entediting 1 0 [ entediting $setting_entediting ]
]

resetlight = [
    lightcolour = 0
    lightrgb = "255 255 255"
    lightbright = 1
    lightradius = 128
]

lightcmd = [
    lightr = (at $lightrgb 0)
    lightg = (at $lightrgb 1)
    lightb = (at $lightrgb 2)
    if (= $lightbright 0) [
        if (strcmp $lightrgb "255 255 255") [
            lightr = 192; lightg = 192; lightb = 192
        ] [
            lightr = (div $lightr 2); lightg = (div $lightg 2); lightb = (div $lightb 2)
         ]
    ]
    result (concat newent light $lightradius $lightr $lightg $lightb)
]

newgui newlight [
    guibutton "sunlight"    "newent light 0 255 200 100"
    guibutton (lightcmd)
    guibar
    guicheckbox "bright"    lightbright
    guitext "color:"
    guiradio "white"        lightcolour 0 [lightrgb = "255 255 255"]
    guiradio "blue"         lightcolour 1 [lightrgb = "192 192 255"]
    guiradio "red"          lightcolour 2 [lightrgb = "255 192 192"]
    guiradio "green"        lightcolour 3 [lightrgb = "192 255 192"]
    guiradio "yellow"       lightcolour 4 [lightrgb = "255 255 192"]
    guiradio "purple"       lightcolour 5 [lightrgb = "255 192 255"]
    guiradio "turquoise"    lightcolour 6 [lightrgb = "192 255 255"]
    guitext "radius:"
    guislider lightradius 0 512
]

newgui newteleport [
    guibutton "newent teleport 1"
    guibutton "newent teledest 1"
    guibutton "newent teleport 2"
    guibutton "newent teledest 2"
    guibutton "newent teleport 3"
    guibutton "newent teledest 3"
    guibutton "newent teleport 4"
    guibutton "newent teledest 4"
]

macro resbutton [
    guibutton "%1x%2" "screenres %1 %2" (if (&& (= $scr_w %1) (= $scr_h %2)) [result "radioboxon"] [result "radiobox"])
]

newgui options [
	guilist [
		guitext "name " player; newname = (getname)
		guifield newname 16 [name $newname]
	]
	guilist [
		guitext "team " team; newteam = (getteam)
		guifield newteam 16 [team $newteam]
	]

	guibar

	guicheckbox "auto connect on start" autoconnect
	guicheckbox "apply changes dialog" applydialog

	guibar
    guibutton "Restore Defaults" "exec defaults.cfg"

    guitab    "gfx"
    guitext "performance key: ^f0fast^f~, ^f2moderate^f~, ^f3slow and pretty^f~"
    guibar
    if (&& (< $shaders 0) (= $renderpath 0)) [
        guicheckbox "^f2shaders			"			shaders 1 -1
	] [
    	guilist [
        	guicheckbox "^f2shaders			"           shaders
        	if $shaders [
            	guibar
            	guiradio "^f0low detail" shaderdetail 1
            	guibar
            	guiradio "^f2high detail" shaderdetail 3
        	]
		]
    ]
    guilist [
        guitext "water			  "
        guibar
        guicheckbox "^f2refraction"  waterrefract
        guibar
        guicheckbox "^f3reflection"  waterreflect
        guibar
        guicheckbox "^f0caustics"    caustics
        if (&& (= $renderpath 0) $waterrefract) [
            guibar
            guicheckbox "^f0fog"   refractfog
        ]
        if $usetexrect [
            guibar
            guicheckbox "^f0animation" vertwater
        ]
    ]
    if (> $renderpath 0) [
        guilist [
            guitext "waterfalls		  "
            guibar
            guicheckbox "^f2refraction" waterfallrefract
            guibar
            guicheckbox "^f0reflection" waterfallenv
        ]
    ]
    if (= $renderpath 0) [
        guilist [
            guicheckbox "^f3shadow maps		"  shadowmap
            if $shadowmap [
                guibar
                guiradio "^f2medium quality" shadowmapsize 9 [blurshadowmap 1]
                guibar
                guiradio "^f3high quality" shadowmapsize 10 [blurshadowmap 2]
            ] [
                guibar
                guicheckbox "^f0blob shadows" blobs
            ]
        ]
        if (>= $maxtmus 3) [
            guicheckbox "^f2dynamic lights	"    ffdynlights 5 0
        ]
    ] [
        guilist [
            guicheckbox "^f3soft shadows	"  shadowmap
            if $shadowmap [
                guibar
                guiradio "^f2medium quality" shadowmapsize 9 [blurshadowmap 1]
                guibar
                guiradio "^f3high quality" shadowmapsize 10 [blurshadowmap 2]
            ] [
                guibar
                guicheckbox "^f0blob shadows" blobs
            ]
        ]
        if $glare [
            glarepreset = 0
            if (= $glarescale 1) [
                if (= $blurglare 4) [glarepreset = 1]
                if (= $blurglare 7) [glarepreset = 3]
            ]
            if (= $glarescale 2) [
                if (= $blurglare 3) [glarepreset = 2]
                if (= $blurglare 7) [glarepreset = 4]
            ]
            guilist [
                guicheckbox "^f3glare			"       glare
                guibar
                guiradio "^f2subtle" glarepreset 1 [blurglare 4; glarescale 1]
                guibar
                guiradio "^f2glowy" glarepreset 2 [blurglare 3; glarescale 2]
                guibar
                guiradio "^f3soft" glarepreset 3 [blurglare 7; glarescale 1]
                guibar
                guiradio "^f3intense" glarepreset 4 [blurglare 7; glarescale 2]
            ]
        ] [
            guicheckbox "^f3glare"         glare
        ]
    ]
    guilist [
        guicheckbox "^f3motion blur 	" motionblur
        if $motionblur [
            guibar
            guiradio "^f3subtle" motionblurscale 0.5
            guibar
            guiradio "^f3moderate" motionblurscale 1
        ]
    ]
    guilist [
        guicheckbox "^f3grass			" grass
        if $grass [
            guibar
            guiradio "^f2quick fade" grassdist 128
            guibar
            guiradio "^f2moderate fade" grassdist 256
            guibar
            guiradio "^f3slow fade" grassdist 512
        ]
    ]
    if (> $renderpath 0) [
        guilist [
            guicheckbox "^f0dynamic lights	"    maxdynlights 3 0
            if $maxdynlights [
                guibar
                guiradio "^f0medium quality" maxdynlights 3
                guibar
                guiradio "^f2high quality" maxdynlights 5
            ]
        ]
        guilist [
            guicheckbox "^f2soft particles	" depthfx
            if $depthfx [
                guibar
                guiradio "^f0low quality" depthfxsize 7 [depthfxrect 0; depthfxfilter 1; blurdepthfx 1]
                guibar
                guiradio "^f2medium quality" depthfxsize 10 [depthfxrect 1; depthfxfilter 0; blurdepthfx 0]
                guibar
                guiradio "^f3high quality" depthfxsize 12 [depthfxrect 1; depthfxfilter 0; blurdepthfx 0]
            ]
        ]
    ]
    guicheckbox "^f0glass reflection"  glassenv
    guilist [
        guicheckbox "^f0decals			" decals
        if $decals [
            guibar
            guiradio "^f0quick fade" decalfade 10000 [maxdecaltris 1024]
            guibar
            guiradio "^f2slow fade" decalfade 60000 [maxdecaltris 4096]
        ]
    ]
    guicheckbox "^f0fix t-joints (world sparklies)" filltjoints
    guilist [
        guitext "textures			  "
        guibar
        guiradio "^f0low quality" maxtexsize 256
        guibar
        guiradio "^f2high quality" maxtexsize 0
    ]
    guilist [
        guitext "models			  "
        guibar
        guicheckbox "^f0lighting"    lightmodels
        guibar
        guicheckbox "^f0reflection"  envmapmodels
        guibar
        guicheckbox "^f0glow"        glowmodels
        if (> $renderpath 0) [
            guibar
            guicheckbox "^f2bumpmap" bumpmodels
        ]
    ]
    guilist [
        guitext "animation		  "
        guibar
        guiradio "^f0medium quality" matskel 1
        guibar
        guiradio "^f0high quality" matskel 0
    ]

    guitab    "display"
    guicheckbox "v-sync"            vsync 1 0
    guicheckbox "fullscreen"        fullscreen
    guitext "gamma (default: 100)"
    guislider gamma
    guitext "full-scene anti-aliasing (default: -1)"
    guilistslider fsaa "-1 0 2 4 8 16"
    guitext "color depth (default: 0)"
    guilistslider colorbits "0 8 16 24 32"
    guitext "z-buffer depth (default: 0)"
    guilistslider depthbits "0 8 16 24 32"
    guitext "stencil bits (default: 1)"
    guislider stencilbits
    guitext "anisotropic filtering (default: 0)"
    guilistslider aniso "0 2 4 8 16"

    guitab   "ui"
    guicheckbox "show crosshair" showcrosshair
    guibar
    guilist [
      guitext "crosshair size: "
      crosshairsizestorage = $crosshairsize
      guifield crosshairsizestorage 6 [crosshairsize $crosshairsizestorage]
      guitext " (0 - 1000, default: 0.05)"
    ]
    guilist [
      guitext "zoom crosshair size: "
      zoomcrosshairsizestorage = $zoomcrosshairsize
      guifield zoomcrosshairsizestorage 6 [zoomcrosshairsize $zoomcrosshairsizestorage]
      guitext " (0 - 1000, default: 0.575)"
    ]
    guibar
    guitext "notice messages"
    guilist [
    guilist [
      guiradio "off" shownotices 0
      guiradio "basic notices" shownotices 1
      guiradio "level 2" shownotices 2
      guiradio "level 3" shownotices 3
      guiradio "everything" shownotices 4
    ]
    guibar
    guilist [
      guitext "fps display"
      guiradio "off" showfps 0
      guiradio "number only" showfps 1
      guiradio "verbose" showfps 2
      guiradio "more verbose" showfps 3
    ]
    ]

    guitab   "res"
    guistayopen [
      guilist [
        guilist [
            guitext "4:3"
            @(resbutton 320 240)
            @(resbutton 640 480)
            @(resbutton 800 600)
            @(resbutton 1024 768)
            @(resbutton 1152 864)
            @(resbutton 1280 960)
            @(resbutton 1400 1050)
            @(resbutton 1600 1200)
            @(resbutton 1792 1344)
            @(resbutton 1856 1392)
            @(resbutton 1920 1440)
            @(resbutton 2048 1536)
            @(resbutton 2800 2100)
            @(resbutton 3200 2400)
        ]
        guibar
        guilist [
            guitext "16:10"
            @(resbutton 320 200)
            @(resbutton 640 400)
            @(resbutton 1024 640)
            @(resbutton 1280 800)
            @(resbutton 1440 900)
            @(resbutton 1600 1000)
            @(resbutton 1680 1050)
            @(resbutton 1920 1200)
            @(resbutton 2048 1280)
            @(resbutton 2560 1600)
            @(resbutton 3840 2400)
        ]
        guibar
        guilist [
            guitext "16:9"
            @(resbutton 1024 600)
            @(resbutton 1280 720)
            @(resbutton 1366 768)
            @(resbutton 1600 900)
            @(resbutton 1920 1050)
            @(resbutton 2048 1152)
            @(resbutton 3840 2160)
        ]
        guibar
        guilist [
            guitext "5:4"
            @(resbutton 600 480)
            @(resbutton 1280 1024)
            @(resbutton 1600 1280)
            @(resbutton 2560 2048)
        ]
        guibar
        guilist [
            guitext "5:3"
            @(resbutton 800 480)
            @(resbutton 1280 768)

            guibar
            guitext "Custom"
            guilist [
                customw = $scr_w
                customh = $scr_h
                guifield customw 4 [scr_w $customw]
                guifield customh 4 [scr_h $customh]
            ]
        ]
      ]
    ]

    guitab   "sound"
    guitext "master volume"
    guislider mastervol
    guitext "sound volume"
    guislider soundvol
    guitext "music volume"
    guislider musicvol
    guitext "sound channels"
    guislider soundchans
    guitext "sound frequency"
    guilistslider soundfreq "11025 22050 44100"
    guitext "sound buffer length"
    guislider soundbufferlen
    guicheckbox "Mumble positional audio" mumble

    guitab  "mouse"
	guilist [
		guilist [
			guitext "mouse input style "
			guitext "mouse overall sensitivity	"
			guitext "mouse yaw sensitivity"
			guitext "mouse pitch sensitivity"
			guistrut 1
			guicheckbox "invert y-axis" mouseinvert
		]
		guilist [
			guilist [
				guiradio "default " mousetype 0
				guiradio "panned " mousetype 1
				guiradio "free " mousetype 1
			]
			guislider sensitivity 1 100
			guislider yawsensitivity 0 100
			guislider pitchsensitivity 1 100
			guistrut 1
			guicheckbox "absolute movements" mouseabsolute
		]
	]

    guitab  "keys"
    guitext "basic keybinds, for anything more use the 'bind' command"
    guitext "select action to bind and press desired keys (ESC when done):"
	guistrut 1
    guilistsplit n 2 $bindactions [
        newbinds = (concatword "newbinds" $i)
        guilist [
            guitext (tabify [@@(concatword (at $bindnames $i) " ")] 4)
            @newbinds = (searchbinds [@@n])
            guikeyfield @newbinds -24 [
                oldbinds = (searchbinds [@@@n])
                loop i (listlen $oldbinds) [bind (at $oldbinds $i) ""]
                loop i (listlen $@@newbinds) [bind (at $@@@newbinds $i) [@@@@n]]
            ]
        ]
    ] [guistrut 2]

    //guitab "autoexec.cfg"
    //guitext "autoexec.cfg"
    //showfileeditor "autoexec.cfg" -60 15
]

bindactions = [
	forward backward left right "universaldelta 1" "universaldelta -1" "spectator 1" "spectator 0"
	"action 0" "action 1" "action 2" "action 3" "action 4" "action 5" "action 6" "action 7"
	drop "weapon 0" "weapon 1" "weapon 2" "weapon 3" "weapon 4" "weapon 5" "weapon 6" "weapon 7"
	"saycommand /" saytextcommand sayteamcommand toggleconsole toggleedit grabinput
	addbot delbot "showgui game" "showgui vote" "setmaster 1" "showgui arena"
]
bindnames = [
	forward backward left right "scroll up" "scroll down" "enter spectator" "exit spectator"
	"attack" "alt-attack" "reload" "use" "jump" "sprint" "crouch" "impulse"
	"drop weapon" melee pistol shotgun smg flamer plasma grenade insta
	"cmd input" "all chat" "team chat" "toggle editing" "toggle console" "toggle input"
	"add bot" "delete bot" "game menu" "vote menu" "claim master" "arena menu"
]

entupdate = [ entset $tmpt $tmp0 $tmp1 $tmp2 $tmp3 ]

initentgui = [
  tmpt = ( et )
  tmp0 = ( ea 0 )
  tmp1 = ( ea 1 )
  tmp2 = ( ea 2 )
  tmp3 = ( ea 3 )
]

genentattributes = [
    entattributes = ""
    n = ( listlen $arg2 )
    loop i $n [
        do [
          t@i = [ guitext @(at $arg2 $i) ]
          a@i = [ guislider tmp@i @(at $arg3 (* 2 $i)) @(at $arg3 (+ 1 (* 2 $i))) entupdate ]
          entattributes = [ @@entattributes @t@@i; @a@@i; ]
        ]
    ]
]

guilistsplit = [
  guilist [
    i = 0
    n = (listlen $arg3)
    z = (div (+ $n (- $arg2 1)) $arg2)
    loop a $arg2 [
      guilist [
        t = (min (+ $i $z) $n)
        while [< $i $t] [
          push $arg1 (at $arg3 $i)
          @@@arg4
          pop $arg1
          i = (+ $i 1)
        ]
      ]
      if (&& (>= $numargs 5) (< (+ $a 1) $arg2)) [@arg5]
    ]
  ]
]

quickeditmenu = [
  guitext "Quick Commands:"
  guibar
  guifield  savemap_name 10 [ savemap $savemap_name ]
  guibutton quicklight          "calclight -1"
  guibutton "optimize map"      "remip"
  guibutton "new entity"        "newent light"
  guibar
  guibutton newmap
  guibar
  guibutton help "showgui editing"
]

matmenu = [
  guibutton "air"                        "editmat air"
  guibutton "water"                      "editmat water"
  guibutton "clip"                       "editmat clip"
  guibutton "glass"                      "editmat glass"
  guibutton "noclip"                     "editmat noclip"
  guibutton "lava"                       "editmat lava"
  guibutton "aiclip"                     "editmat aiclip"
  guibutton "death"                      "editmat death"
  guibar
  guicheckbox "show material volumes"    showmat
]

newentgui = [
  do [
    genentattributes [@@arg1] [@@arg2] [@@arg3]
    newgui @arg1 [
      guitext $tmpt
      guibar
      @entattributes
      guitab type
      guilistsplit n 2 $enttypelist [
          guibutton $n [ entset @n ]
      ]
      guitab misc
      @quickeditmenu
    ]
  ]
]

loop i $enttypelength [
  newentgui (at $enttypelist $i) "" ""
]

newgui materials [
  @matmenu
  guitab misc
  @quickeditmenu
]

newgui quickedit [
  @quickeditmenu
  guitab materials
  @matmenu
]

newentgui light "radius red green blue" "0 400 0 255 0 255 0 255"
newentgui spotlight "radius" "0 200"
newentgui playerstart "yaw team id" "0 360"
newentgui teleport "tag" "0 20"
newentgui teledest "yaw tag" "0 360 0 20"
newentgui monster "yaw type" "0 360 0 7"
newentgui mapmodel "model yaw type tag flags" "360 0 0 100 0 29 0 20"
newentgui envmap "radius" "0 400"
newentgui pusher "Z Y X" "0 200 0 200 0 200"
newentgui carrot "tag type" "0 50 0 29"
newentgui sound "type radius size" "0 20 0 500 0 500"
newentgui particles "type" "0 3"

guilistloop = [
	i = 0
	j = (+ (div $arg3 (* $arg1 $arg2)) 1)
	loop a $j [
		if (> $a 0) [ guitab $a ]
		guilist [
			loop b $arg1 [
				guilist [
					loop c $arg2 [
						if (< $i $arg3) [
							@@arg4; i = (+ $i 1)
							if (< $c (- $arg2 1)) [ guibar ]
						]
					]
				]
				if (&& (< $i $arg3) (< $b (- $arg1 1))) [ guibar ]
			]
		]
	]
]

alias showmapmodel [
	mapmodelpath = (mapmodelindex $arg1)
	mapmodelshot = (concatword "models/" $mapmodelpath "/thumb")
	mapmodelcmd = (concat newent mapmodel $arg1)
	guilist [ guilist [
		guiimage $mapmodelshot $mapmodelcmd 2 1 "textures/nothumb"
		guibutton $mapmodelpath $mapmodelcmd
	] ]
]

newgui mapmodels [
	mapmodelnum = (mapmodelindex)
	guilistloop 4 2 $mapmodelnum [ showmapmodel $i ]
]
