// compass/voice actions

voices = 0
addvoice = [
	voices = (+ $voices 1)
	do [@(format "voice%1str" $voices) = @arg1; @(format "voice%1snd" $voices) = @arg2]
	result $arg2 // so we can store and reuse ids
]

vsnd1 = (addvoice "argh" (registersound "player/death1" 200 "air" -1 -1 1))
vsnd2 = (addvoice "lucky" (registersound "player/pain1" 200 "air" -1 -1 1))
vsnd3 = (addvoice "pzap" (registersound "player/pain2" 200 "air" -1 -1 1))
vsnd4 = (addvoice "ns" (registersound "player/pain4" 200 "air" -1 -1 1))
vsnd5 = (addvoice "boom" (registersound "player/pain5" 200 "air" -1 -1 1))
vsnd6 = (addvoice "damn" (registersound "player/death2" 200 "air" -1 -1 1))
vsnd7 = (addvoice "haha" (registersound "player/pain6" 200 "air" -1 -1 1))
vsnd8 = (addvoice "suck" (registersound "player/jump" 200 "air" -1 -1 1))
addvoice "nice" $vsnd3
addvoice "gg~" $vsnd2
addvoice "good game" $vsnd2
addvoice "sorry" $vsnd1
addvoice "sry" $vsnd1
addvoice "yes" $vsnd4
addvoice "no problem" $vsnd5
addvoice "np" $vsnd5
addvoice "defend" $vsnd7
addvoice "ready" $vsnd8
addvoice "go go" $vsnd1
addvoice "hang on" $vsnd5
addvoice "no" $vsnd6
addvoice "rofl" $vsnd7
addvoice "lol" $vsnd7
addvoice "lmfao" $vsnd7
addvoice "heh" $vsnd7

on_text = [
	id = 0
	reg = (format "%1~" $arg4) // the ~ allows absolute matching, like in gg
	loopwhile i $voices [= $id 0] [
		str = (getalias (format "voice%1str" (+ $i 1)))
		if (strncasecmp $str $reg (strlen $str)) [ id = (+ $i 1) ]
	]
	result (getalias (format "voice%1snd" $id))
]
